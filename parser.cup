/* Simple +/-/* expression language; parser evaluates constant expressions on the fly*/
/**
 *  Package and Import Specifications
 */
import java_cup.runtime.*;

/**
 *  Usercode Components
 */
parser code {:
    // Connect this parser to a scanner!
    Scanner s;
    Parser(Scanner s){ this.s=s; }
:}

/* define how to connect to the scanner! */
scan with {: return s.next_token(); :};

/**
 *  Symbol List
 */

/* Terminals (tokens returned by the scanner). */
terminal            SEMI, PLUS, LPAREN, RPAREN, LBRACK, RBRACK, COMMA;
terminal String     STRING_LITERAL, IDENTIFIER;

/*  Non terminals */
non terminal            program;
non terminal String    declaration, func_decl,func_body, func_call, arg,call_arg, expression, stmt;      // used to store evaluated subexpressions

/**
 *  Precedence Declarations
 */
precedence left PLUS, COMMA;
// precedence right EXP;

/**
 *  The Grammar Rules
 */

program ::= declaration:d expression:e {:
			System.out.println("public class Main {");
            System.out.printf("%s", d);
			System.out.println("\tpublic static void main(String[] args) {");
			System.out.printf("%s", e);
			System.out.println("\t}");
			System.out.println("}");
			 :}
	;

declaration ::= declaration:rest func_decl:d          {: RESULT = String.format("%s\n %s\n", rest, d);:}
            |                                         {: RESULT = ""; :}
            ;

func_decl ::= IDENTIFIER:name LPAREN arg:arg RPAREN LBRACK func_body:s RBRACK       {: RESULT = String.format("\tpublic static String %s(%s) {\n\t\treturn %s;\n\t}", name, arg, s); :}
            | IDENTIFIER:name LPAREN RPAREN LBRACK func_body:s RBRACK                       {: RESULT = String.format("\tpublic static String %s() {\n\t\treturn %s;\n\t}", name, s); :}
            ;

arg ::= arg:a1 COMMA arg:a2               {: RESULT = String.format("%s, %s", a1, a2); :}
        | IDENTIFIER:id               {: RESULT = String.format("String %s", id); :}
        ;

func_body ::= STRING_LITERAL:s             {: RESULT = String.format("\"%s\"", s); :} 
            | IDENTIFIER:id                 {: RESULT = id; :}
            | func_body:s1 PLUS func_body:s2 {: RESULT = String.format("%s + %s", s1, s2); :} 
            | func_call:f             {: RESULT=f; :}
            ;

func_call ::= IDENTIFIER:name LPAREN call_arg:arg RPAREN          {: RESULT = String.format("%s(%s)", name, arg); :}
            | IDENTIFIER:name LPAREN RPAREN                      {: RESULT = String.format("%s()", name); :}
            ;
call_arg ::= call_arg:s1 COMMA call_arg:s2                        {: RESULT = String.format("%s, %s", s1, s2); :}
            | stmt:s                              {:   RESULT = s; :}
            ;




expression ::= expression:rest stmt:s    {: RESULT = String.format("%s\t\tSystem.out.println(%s);\n", rest, s); :}
            | stmt:s                              {: RESULT = String.format("\t\tSystem.out.println(%s);\n",s);; :}
            ;
stmt      ::= STRING_LITERAL:s             {: RESULT = String.format("\"%s\"", s); :}  
            | stmt:s1 PLUS stmt:s2 {: RESULT = String.format("%s + %s", s1, s2); :} 
            | func_call:f             {: RESULT=f; :}
            ;
